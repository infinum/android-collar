plugins {
    id "java-gradle-plugin"
    id "org.jetbrains.kotlin.jvm"
    id "com.gradle.plugin-publish" version "1.2.0"
}

kotlin {
    explicitApi()
}

java {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
}

compileKotlin {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
        freeCompilerArgs += [
                '-Xjvm-default=all'
        ]
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }
}

dependencies {
    implementation libs.kotlin.core
    implementation libs.kotlin.reflect
    implementation libs.librarygenerator
    compileOnly gradleApi()
    compileOnly libs.tools.gradle

    constraints {
        implementation(libs.kotlin.reflect) {
            because("""Android Gradle Plugin 4.1.1 depends on Kotlin 1.3.72 but we should not mix 1.3 and 1.4.
                This constraint should be lifted on Android Gradle Plugin 4.2.0. See
                https://dl.google.com/android/maven2/com/android/tools/build/gradle/4.2.0-beta02/gradle-4.2.0-beta02.pom
            """)
        }
    }
}

apply from: "publish.gradle"

def versionDirectory = "$buildDir/generated/version/"

sourceSets {
    main.java.srcDir versionDirectory
}

task pluginVersion {
    def outputDir = file(versionDirectory)

    inputs.property 'version', releaseConfig.version
    outputs.dir outputDir

    doLast {
        def versionFile = file("$outputDir/com/infinum/collar/plugin/version.kt")
        versionFile.parentFile.mkdirs()
        versionFile.text = """// Generated file. Do not edit!
package com.infinum.collar.plugin
internal const val collarVersion = "${releaseConfig.version}"
"""
    }
}

tasks.getByName('compileKotlin').dependsOn('pluginVersion')
