apply plugin: 'maven-publish'
apply plugin: "com.jfrog.bintray"

task sourcesJar(type: Jar, dependsOn: assemble) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.kotlin.srcDirs
}

task javadocsJar(type: Jar, dependsOn: "dokkaJavadoc") {
    archiveClassifier.set("javadoc")
    from dokkaJavadoc.outputDirectory
}

afterEvaluate {
    publishing {
        publications {
            infinum(MavenPublication) {
                groupId = collar.group
                artifactId = 'collar-ui-no-op'
                version = collar.version

                artifact sourcesJar
                artifact javadocsJar

                pom {
                    name = 'Collar UI no op'
                    description = 'No op UI which provides single point for analytics collection preview.'
                    url = "https://github.com/infinum/android-collar"
                    licenses {
                        license {
                            name = "Apache License 2.0"
                            url = "https://github.com/infinum/android-collar/blob/master/LICENSE"
                        }
                    }
                    developers {
                        developer {
                            id = "knobtviker"
                            name = "Bojan KomljenoviÄ‡"
                            email = "bojan.komljenovic@infinum.com"
                        }
                    }
                    scm {
                        connection = "https://github.com/infinum/android-collar.git"
                        developerConnection = "https://github.com/infinum/android-collar.git"
                        url = "https://github.com/infinum/android-collar"
                    }
                }
                pom.withXml {
                    def root = asNode()
                    def dependenciesNode = root.appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    Properties properties = new Properties()
    def bintrayProperties = rootProject.file('bintray.properties')
    if (bintrayProperties.exists()) {
        properties.load(new FileInputStream(bintrayProperties))
    } else {
        properties.setProperty("bintray.user", "")
        properties.setProperty("bintray.apikey", "")
    }

    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    publications = ["infinum"]

    pkg {
        repo = 'android'
        name = 'collar-ui-no-op'
        userOrg = 'infinum'
        desc = 'Collar no op UI packages'
        websiteUrl = 'https://github.com/infinum/android-collar'
        issueTrackerUrl = 'https://github.com/infinum/android-collar/issues'
        vcsUrl = 'https://github.com/infinum/android-collar.git'
        licenses = ['Apache-2.0']
        version {
            name = "${collar.version}"
            released = new Date()
            vcsTag = "${collar.version}"
        }
        publish = true
        override = true
        publicDownloadNumbers = true
    }
}
