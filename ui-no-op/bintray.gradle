apply plugin: 'maven-publish'
apply plugin: "com.jfrog.bintray"
apply plugin: 'com.github.dcendents.android-maven'

Properties properties = new Properties()
def bintrayProps = rootProject.file('bintray.properties')
if (bintrayProps.exists()) {
    properties.load(new FileInputStream(file(bintrayProps)))
}

group = collar.group
version = collar.version

task sourcesJar(type: Jar, dependsOn: assemble) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.kotlin.srcDirs
}

task javadocsJar(type: Jar, dependsOn: rootProject.dokka) {
    archiveClassifier.set('javadoc')
    from rootProject.dokka.outputDirectory
}

publishing {
    publications {
        infinum(MavenPublication) {
            groupId = group
            artifactId = 'collar-ui-no-op'

            artifact sourcesJar
            artifact javadocsJar
            pom {
                name = 'Collar UI no op'
                description = 'No op UI which provides single point for analytics collection preview.'
                url = 'https://github.com/infinum/android-collar'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
            pom.withXml {
                def root = asNode()
                def dependenciesNode = root.appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    pkg {
        repo = 'android'
        name = 'collar-ui-no-op'
        userOrg = 'infinum'
        desc = 'Collar no op UI packages'
        websiteUrl = 'https://github.com/infinum/android-collar'
        issueTrackerUrl = 'https://github.com/infinum/android-collar/issues'
        vcsUrl = 'https://github.com/infinum/android-collar.git'
        licenses = ['Apache-2.0']
        version {
            name = "${collar.version}"
            released = new Date()
            vcsTag = "${collar.version}"
        }
        publish = true
        override = true
        publicDownloadNumbers = true
    }

    configurations = ['archives']
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'jar'
                groupId 'co.infinum.collar'
                artifactId 'collar-ui-no-op'

                name 'collar-ui-no-op'
                description 'Collar UI no op'
                url 'https://github.com/infinum/android-collar'

                licenses {
                    license {
                        name 'Apache'
                        url 'https://github.com/infinum/android-collar/LICENSE'
                    }
                }
                developers {
                    developer {
                        id 'knobtviker'
                        name 'Bojan KomljenoviÄ‡'
                        email 'bojan.komljenovic@infinum.hr'
                    }
                }
                scm {
                    connection 'https://github.com/infinum/android-collar.git'
                    developerConnection 'https://github.com/infinum/android-collar.git'
                    url 'https://github.com/infinum/android-collar'
                }
            }
        }
    }
}