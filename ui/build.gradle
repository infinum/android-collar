plugins {
    id "com.android.library"
    id "kotlin-android"
    id "com.google.devtools.ksp" version "2.1.20-1.0.31"
}

android {
    buildFeatures {
        buildConfig = true
        viewBinding = true
    }

    compileSdkVersion buildConfig.compileSdk

    defaultConfig {
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.incremental": "true",
                ]
            }
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled true
            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.txt"
        }
    }

    namespace "com.infinum.collar.ui"
    resourcePrefix 'collar_'

    kotlin {
        jvmToolchain(8)
    }
    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(8))
        }
    }

    lint {
        disable 'RtlEnabled', 'VectorPath', 'IconLocation'
    }
    kotlinOptions {
        freeCompilerArgs += [
                '-Xjvm-default=all',
                '-Xexplicit-api=strict',
                '-opt-in=kotlinx.coroutines.DelicateCoroutinesApi'
        ]
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude("META-INF/*.kotlin_module")
    }
    testOptions {
        animationsDisabled = true
    }

    sourceSets.each {
        it.java.srcDirs += "src/$it.name/kotlin"
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
    testNamespace 'com.infinum.collar'
}

dependencies {
    implementation libs.kotlin.core
    implementation libs.coroutines
    api libs.librarycore
    implementation libs.bundles.androidx
    ksp libs.androidx.room.compiler
    implementation libs.material
    ksp libs.inject.compiler
    implementation libs.inject.runtime

    testImplementation libs.junit
    testImplementation 'org.mockito:mockito-core:3.5.7'

    androidTestImplementation 'androidx.arch.core:core-testing:2.1.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test:core-ktx:1.3.0'
    androidTestImplementation 'org.mockito:mockito-android:3.3.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.3.0'
//    testImplementation 'androidx.test.espresso:espresso-core:3.3.0'
//    testImplementation 'androidx.test.espresso:espresso-intents:3.3.0'
}

ksp {
    arg("me.tatarka.inject.dumpGraph", "false")
}

apply from: "publish.gradle"

project.gradle.taskGraph.whenReady {
    connectedDebugAndroidTest {
        ignoreFailures = true
    }
}