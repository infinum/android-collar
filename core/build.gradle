apply plugin: "com.android.library"
apply plugin: 'com.archinamon.aspectj-provides'
apply plugin: 'maven-publish'

android {
    compileSdkVersion buildConfig.compileSdk
    buildToolsVersion buildConfig.buildTools

    defaultConfig {
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk
        versionCode 1
        versionName "1.0.0"
    }

    buildTypes {
        release {
            minifyEnabled true
            consumerProguardFiles "consumer-proguard-rules.pro"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compileOnly packages.annotations

    testImplementation packages.junit
    testImplementation packages.mockito
    testImplementation packages.truth
}

group = 'co.infinum.collar'
version = '1.0.0'

task sourcesJarCore(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'collar-core'
            artifact sourcesJarCore
            artifact javadocJar
            pom {
                name = 'Collar Core'
                description = 'Gradle plugin which provides single point for analytics collection.'
                url = 'https://github.com/infinum/Android-Collar'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }
}

aspectj {
    ajc '1.9.4' // default value
    debugInfo true
    weaveInfo true
    ajcArgs << '-referenceInfo' << '-warn:deprecation' << '-verbose'
}

apply from: "../tasks.gradle"
apply from: "../bintray.gradle"

//preBuild.dependsOn ':core:generateReadme'