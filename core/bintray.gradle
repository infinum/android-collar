apply plugin: 'maven-publish'
apply plugin: "com.jfrog.bintray"
apply plugin: 'com.github.dcendents.android-maven'

Properties properties = new Properties()
properties.load(new FileInputStream(file(rootProject.file('bintray.properties'))))

group = collar.group
version = collar.version

task sourcesJar(type: Jar, dependsOn: assemble) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.source
}

task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.source
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
    archiveClassifier.set('javadoc')
    from javadocs.destinationDir
}

publishing {
    publications {
        Publication(MavenPublication) {
            artifactId = 'collar-core'
            artifact sourcesJar
            artifact javadocsJar
            pom {
                name = 'Collar Core'
                description = 'Gradle plugin which provides single point for analytics collection.'
                url = 'https://github.com/infinum/android-collar'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
            pom.withXml {
                def root = asNode()
                def dependenciesNode = root.appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    pkg {
        repo = 'android'
        name = 'collar-core'
        userOrg = 'infinum'
        desc = 'Collar Core package'
        websiteUrl = 'https://github.com/infinum/android-collar'
        issueTrackerUrl = 'https://github.com/infinum/android-collar/issues'
        vcsUrl = 'https://github.com/infinum/android-collar.git'
        licenses = ['Apache-2.0']
        version {
            name = "${versions.collar}"
            released = new Date()
            vcsTag = "${versions.collar}"
            gpg {
                sign = true
                passphrase = properties.getProperty("bintray.gpg_passphrase")
            }
        }
        publish = true
        override = true
        publicDownloadNumbers = true
    }

    configurations = ['archives']
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                groupId 'co.infinum.collar'
                artifactId 'collar-core'

                name 'collar-core'
                description 'Collar Core'
                url 'https://github.com/infinum/android-collar'

                licenses {
                    license {
                        name 'Apache'
                        url 'https://github.com/infinum/android-collar/LICENSE'
                    }
                }
                developers {
                    developer {
                        id 'knobtviker'
                        name 'Bojan KomljenoviÄ‡'
                        email 'bojan.komljenovic@infinum.hr'
                    }
                }
                scm {
                    connection 'https://github.com/infinum/android-collar.git'
                    developerConnection 'https://github.com/infinum/android-collar.git'
                    url 'https://github.com/infinum/android-collar'

                }
            }
        }
    }
}